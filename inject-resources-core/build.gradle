plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.8.5'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = '@InjectResources core'
                description = 'Core of @InjectResources'
                url = 'https://github.com/hosuaby/inject-resources'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hosuaby'
                        name = 'Alexei KLENIN'
                        email = 'alexei.klenin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:hosuaby/inject-resources.git'
                    developerConnection = 'scm:git:git@github.com:hosuaby/inject-resources.git'
                    url = 'https://github.com/hosuaby/inject-resources'
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'InjectResources'
        name = 'InjectResources'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/hosuaby/inject-resources'
        version {
            name = project.version
            released  = new Date()
            vcsTag = 'v' + project.version
        }
    }
    publications = ['mavenJava']
    publish = true
}

def JUNIT_VERSION = '5.6.0'
def JACKSON_VERSION = '2.10.3'
def GSON_VERSION = '2.8.0'

dependencies {
    compile group: 'org.jetbrains', name: 'annotations', version: '19.0.0'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: JUNIT_VERSION

    testImplementation project(':test-commons')
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.14.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: JUNIT_VERSION
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: JACKSON_VERSION
    testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: JACKSON_VERSION
    testImplementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-properties', version: '2.11.0.rc1'
    testImplementation group: 'com.google.code.gson', name: 'gson', version: GSON_VERSION
    testImplementation group: 'org.apache.commons', name: 'commons-csv', version: '1.8'
    testImplementation group: 'de.siegmar', name: 'fastcsv', version: '1.0.3'

    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: JUNIT_VERSION
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
