plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.8.5'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jar {
    from project.sourceSets.main.output
    from project(':inject-resources-commons').sourceSets.main.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = '@InjectResources Spring'
                description = '@InjectResources Spring module'
                url = 'https://github.com/hosuaby/inject-resources'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'hosuaby'
                        name = 'Alexei KLENIN'
                        email = 'alexei.klenin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:hosuaby/inject-resources.git'
                    developerConnection = 'scm:git:git@github.com:hosuaby/inject-resources.git'
                    url = 'https://github.com/hosuaby/inject-resources'
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'InjectResources'
        name = 'InjectResources'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/hosuaby/inject-resources'
        version {
            name = project.version
            released  = new Date()
            vcsTag = 'v' + project.version
        }
    }
    publications = ['mavenJava']
    publish = true
}

def JUNIT_VERSION = '5.6.0'

dependencies {
    compile project(':inject-resources-core')

    compileOnly project(':inject-resources-commons')
    compileOnly group: 'org.springframework', name: 'spring-context'
    compileOnly group: 'org.yaml', name: 'snakeyaml', version: '1.26'

    implementation group: 'net.oneandone.reflections8', name: 'reflections8', version: '0.11.7'

    implementation enforcedPlatform('org.springframework.boot:spring-boot-dependencies:2.3.2.RELEASE')
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: JUNIT_VERSION
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: JUNIT_VERSION
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.14.0'
    testCompile group: 'io.leangen.geantyref', name: 'geantyref', version: '1.3.11'

    testImplementation project(':test-commons')
    testImplementation project(':inject-resources-core')
    testImplementation project(':inject-resources-commons')
    testImplementation group: 'org.yaml', name: 'snakeyaml', version: '1.26'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-testkit', version: '1.6.0'
    testImplementation group: 'com.tngtech.archunit', name: 'archunit', version: '0.14.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
